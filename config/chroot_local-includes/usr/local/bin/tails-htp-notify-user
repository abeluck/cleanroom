#!/usr/bin/perl

use strict;
use warnings;

#man{{{

=head1 NAME

tails-htp-notify-user

=head1 VERSION

Version X.XX

=head1 AUTHOR

T(A)ILS dev team <amnesia@boum.org>
See https://amnesia.boum.org/.

=cut

#}}}

use Desktop::Notify;
use Locale::gettext;
use POSIX;

### initialization
setlocale(LC_MESSAGES, "");
textdomain("tails-htp-notify-user");
my $htp_done_file='/var/lib/live/htp-done';

### main

my $notify = Desktop::Notify->new();

my $summary = gettext("Synchronizing the system's clock");
my $body    = gettext("Tor needs an accurate clock to work properly. Please wait...");

my $notification = $notify->create(summary => $summary,
                                   body => $body,
                                   timeout => 0);

exit 0 if -e $htp_done_file;
$notification->show();
until (-e $htp_done_file) {
    sleep 1;
}

# read the return code from /var/lib/live/htp-done
open(my $htp_done, "< $htp_done_file") or die "Can not read file: $!";
(my $htp_done_code) = (<$htp_done> =~ /^(\d+)/);
# in case htpdate went fine, close the 'Please wait...' notification
if ($htp_done_code == 0) {
    $notification->close();
}
# in case htpdate failed, notify the user with the corresponding logs
else {
    open(my $htp_log, "< /var/log/htpdate.log") or die "Can not read file: $!";
    my $last_log;
    while (<$htp_log>) {
        if ($_ =~ /Running htpdate./) {
            $last_log = ''; 
            next;            
        }
        $last_log .= $_;
    }
    my $failure_summary      = gettext("Failed to synchronize the clock!");
    my $failure_body         = $last_log;
    my $failure_notification = $notify->create(summary => $failure_summary,
                                               body    => $failure_body,
                                               timeout => 0);
    $failure_notification->show();
}

