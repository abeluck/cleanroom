#!/usr/bin/perl

use strict;
use warnings;

#man{{{

=head1 NAME

tails-htp-notify-user

=head1 VERSION

Version X.XX

=head1 AUTHOR

T(A)ILS dev team <amnesia@boum.org>
See https://amnesia.boum.org/.

=cut

#}}}

use Desktop::Notify;
use English '-no_match_vars';
use Locale::gettext;
use POSIX;

### initialization
setlocale(LC_MESSAGES, "");
textdomain("tails-htp-notify-user");
my $htp_done_file = '/var/lib/live/htp-done';
my $htp_log_file  = '/var/log/htpdate.log';

### main

my $notify = Desktop::Notify->new()
    or die "Failed creating Desktop::Notify object.";

my $summary = gettext("Synchronizing the system's clock");
my $body    = gettext("Tor needs an accurate clock to work properly. Please wait...");

my $notification = $notify->create(summary => $summary,
                                   body => $body,
                                   timeout => 0)
    or die "Failed to create notification object";

exit 0 if -e $htp_done_file;
$notification->show() or warn "Failed showing notification.";
until (-e $htp_done_file) {
    sleep 1;
}

# read the return code from /var/lib/live/htp-done
open(my $htp_done, '<', $htp_done_file)
    or die "Can not open file '$htp_done_file': $OS_ERROR";
(my $htp_done_code) = (<$htp_done> =~ /^(\d+)/);
# in case htpdate went fine, close the 'Please wait...' notification
if ($htp_done_code == 0) {
    $notification->close();
}
# in case htpdate failed, notify the user with the corresponding logs
else {
    open(my $htp_log, '<', $htp_log_file)
        or die "Can not open file '$htp_log_file': $OS_ERROR";
    my $last_log;
    while (<$htp_log>) {
        if ($_ =~ /Running htpdate./) {
            $last_log = '';
            next;
        }
        $last_log .= $_;
    }
    my $failure_summary      = gettext("Failed to synchronize the clock!");
    my $failure_body         = $last_log;
    my $failure_notification = $notify->create(summary => $failure_summary,
                                               body    => $failure_body,
                                               timeout => 0);
    $failure_notification->show();
}

